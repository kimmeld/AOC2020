CALL S.GET.INPUT(2020,6,INPUT.LINES)
INPUT.LINES.COUNT = DCOUNT(INPUT.LINES,@FM)

* Part 1
PART.1.TOTAL = 0
THIS.GROUP = ''
FOR X = 1 TO INPUT.LINES.COUNT + 1
  IF INPUT.LINES<X> = '' THEN
    GROUP.ANS = ''
    THIS.GROUP.COUNT = LEN(THIS.GROUP)
    FOR Y = 1 TO THIS.GROUP.COUNT
      THIS.ANS = THIS.GROUP[Y,1]
      LOCATE THIS.ANS IN GROUP.ANS SETTING LOC ELSE
        GROUP.ANS<-1> = THIS.ANS
      END
    NEXT Y
    PART.1.TOTAL += DCOUNT(GROUP.ANS,@FM)

    THIS.GROUP = ''
  END
  ELSE
    THIS.GROUP := INPUT.LINES<X>
  END
NEXT
CRT 'Part 1 = ':PART.1.TOTAL


* Part 2
PART.2.TOTAL = 0
THIS.GROUP = ''
FOR X = 1 TO INPUT.LINES.COUNT + 1
  IF INPUT.LINES<X> = '' THEN
    THIS.GROUP.COUNT = DCOUNT(THIS.GROUP,@FM)
    THIS.GROUP.ANS = '' ;* <1,X> = QUESTION, <2,X> = COUNT
    FOR Y = 1 TO THIS.GROUP.COUNT
      THIS.ANS = THIS.GROUP<Y>
      THIS.ANS.COUNT = LEN(THIS.ANS)
      FOR Z = 1 TO THIS.ANS.COUNT
        LOCATE THIS.ANS[Z,1] IN THIS.GROUP.ANS<1> BY 'AL' SETTING LOC THEN
          THIS.GROUP.ANS<2,LOC> += 1
        END
        ELSE
          INS THIS.ANS[Z,1] BEFORE THIS.GROUP.ANS<1,LOC>
          INS 1 BEFORE THIS.GROUP.ANS<2,LOC>
        END
      NEXT Z
    NEXT Y

    THIS.GROUP.ANS.COUNT = DCOUNT(THIS.GROUP.ANS<1>, @VM)
    FOR Y = 1 TO THIS.GROUP.ANS.COUNT
      IF THIS.GROUP.ANS<2,Y> = THIS.GROUP.COUNT THEN
        PART.2.TOTAL += 1
      END
    NEXT Y

    THIS.GROUP = ''
  END
  ELSE
    THIS.GROUP<-1> = INPUT.LINES<X>
  END
NEXT

CRT 'Part 2 = ':PART.2.TOTAL
