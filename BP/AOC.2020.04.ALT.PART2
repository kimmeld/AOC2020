SUBROUTINE PART2(RESULT, REC)
VALID.ECL = 'amb,blu,brn,gry,grn,hzl,oth'
CONVERT ',' TO @FM IN VALID.ECL

IS.VALID = @TRUE

* Validate byr
BYR = REC<1>
IF NOT(BYR MATCHES '4N' AND BYR >= 1920 AND BYR <= 2002) THEN
  IS.VALID = @FALSE
END

* validate iyr
IYR = REC<2>
IF NOT(IYR MATCHES '4N' AND IYR >= 2010 AND IYR <= 2020) THEN
  IS.VALID = @FALSE
END

* validate eyr
EYR = REC<3>
IF NOT(EYR MATCHES '4N' AND EYR >= 2020 AND EYR <= 2030) THEN
  IS.VALID = @FALSE
END

* Validate hgt
HGT = REC<4>
HGT.UNIT = HGT[2]
HGT.VAL = HGT[1,LEN(HGT)-2]
BEGIN CASE
  CASE HGT.UNIT = 'in'
    IF NOT(HGT.VAL >= 59 AND HGT.VAL <= 76) THEN
      IS.VALID = @FALSE
    END
  CASE HGT.UNIT = 'cm'
    IF NOT(HGT.VAL >= 150 AND HGT.VAL <= 193) THEN
      IS.VALID = @FALSE
    END
  CASE @TRUE
    IS.VALID = @FALSE
END CASE

* Validate hcl
HCL = REC<5>
IF HCL[1,1] = '#' AND LEN(HCL) = 7 THEN
  FOR Y = 2 TO 7
    HCL.CHAR = HCL[Y,1]
    IF NOT((HCL.CHAR >= 0 AND HCL.CHAR <= 9) OR (HCL.CHAR >= 'a' AND HCL.CHAR <= 'f')) THEN
      IS.VALID = @FALSE
    END
  NEXT
END
ELSE
  IS.VALID = @FALSE
END

* Validate ecl
ECL = REC<6>
LOCATE ECL IN VALID.ECL SETTING LOC ELSE
  IS.VALID = @FALSE
END

* Validate PID
PID = REC<7>
IF NOT(PID MATCHES '9N') THEN
  IS.VALID = @FALSE
END
 
RESULT = IS.VALID
