SUBROUTINE S.GET.INPUT(YEAR, DAY, RESULT)
URL = "https://adventofcode.com/":YEAR:"/day/":DAY:"/input"
K.INPUT = YEAR:'-':DAY

OPEN 'AOC.CONFIG' TO F.AOC.CONFIG ELSE 
  STOP 'CANNOT OPEN AOC.CONFIG'
END
READ S.SESSION.COOKIE FROM F.AOC.CONFIG,'SESSION.COOKIE' ELSE
  STOP 'NO SESSION.COOKIE RECORD'
END
OPEN 'AOC.INPUT.FILES' TO F.AOC.INPUT ELSE
  STOP 'CANNOT OPEN AOC.INPUT'
END

* Do we want test data?
GET(ARG.) TEST.KEY THEN
* We do...
  K.INPUT = YEAR:'-':DAY:'-':TEST.KEY
  READ INPUT.DATA FROM F.AOC.INPUT,K.INPUT THEN
    RESULT = INPUT.DATA
  END
  ELSE
    PRINT 'No test data found - ':K.INPUT:' - invoking editor...'
    EXECUTE 'AE AOC.INPUT.FILES ':K.INPUT
    READ INPUT.DATA FROM F.AOC.INPUT,K.INPUT THEN
      RESULT = INPUT.DATA
    END
  END
END
ELSE
  READ INPUT.DATA FROM F.AOC.INPUT,K.INPUT THEN
    RESULT = INPUT.DATA
  END
  ELSE
 * Init security context, create and submite request
    RET = createSecurityContext(MYCTX, "SSLv3:min,TLSv1:no,TLSv1.2:max")
    RET = createSecureRequest(URL, 'GET', REQ, MYCTX)
    RET = setRequestHeader(REQ, 'Cookie', 'session=':S.SESSION.COOKIE)
    RET = submitRequest(REQ, 10000, '', RESP.HEADERS, RESP.DATA, RESP.STATUS)
RESP.DATA = TRIM(RESP.DATA, CHAR(10), 'T')

    WRITE RESP.DATA TO F.AOC.INPUT,K.INPUT
    READ RESP.DATA FROM F.AOC.INPUT,K.INPUT ELSE
      STOP 'CANNOT REREAD DATA'
    END
    RESULT = RESP.DATA
  END
END
