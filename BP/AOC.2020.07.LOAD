CALL S.GET.INPUT(2020,7,INPUT.LINES)
INPUT.LINES.COUNT = DCOUNT(INPUT.LINES, @FM)

OPEN 'AOC.2020.07.BAGS' TO F.BAGS ELSE
  STOP 'Cannot open AOC.2020.07.BAGS'
END

CLEARFILE F.BAGS


FOR X = 1 TO INPUT.LINES.COUNT
  BAG.DATA = CHANGE(INPUT.LINES<X>, ' bags contain ', @FM)
  CONVERT ',' TO @VM IN BAG.DATA
  CONVERT '.' TO '' IN BAG.DATA

  BAG.TYPE = BAG.DATA<1>
  BAG.CONTENTS = RAISE(BAG.DATA<2>)
  BAG.CONTENTS.COUNT = DCOUNT(BAG.CONTENTS,@FM)

  BAG.REC = ''
  BAG.REC<1> = BAG.TYPE

  IF BAG.CONTENTS <> 'no other bags' THEN
    FOR Y = 1 TO BAG.CONTENTS.COUNT
      CONTENT = TRIM(BAG.CONTENTS<Y>)
      QTY = FIELD(CONTENT,' ',1)
      COLOUR =  FIELD(CONTENT,' ',2,999)
      COLOUR = CHANGE(COLOUR,' bags','')
      COLOUR = CHANGE(COLOUR,' bag','')
      BAG.REC<2,-1> = QTY
      BAG.REC<3,-1> = COLOUR
    NEXT
  END

  WRITE BAG.REC TO F.BAGS, BAG.TYPE ELSE
    STOP 'Cannot write to F.BAGS'
  END
NEXT

* For each bag, work out what bag contains it
EXECUTE 'SELECT AOC.2020.07.BAGS' RTNLIST L.BAGS
DONE = @FALSE
LOOP
  READNEXT K.BAGS FROM L.BAGS ELSE
    DONE = @TRUE
  END
UNTIL DONE
  READU R.BAG FROM F.BAGS, K.BAGS THEN
    EXECUTE 'SELECT AOC.2020.07.BAGS WITH CONTAINS.COLOUR = "':K.BAGS:'"'
    READLIST CONTAINED.IN ELSE
      CONTAINED.IN = ''
    END
    R.BAG<4> = LOWER(CONTAINED.IN)
    WRITE R.BAG TO F.BAGS, K.BAGS
  END

REPEAT
