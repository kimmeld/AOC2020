CALL S.GET.INPUT(2020,15,INPUT.DATA)
CONVERT ',' TO @FM IN INPUT.DATA
INPUT.DATA.COUNT = DCOUNT(INPUT.DATA,@FM)

OPEN 'AOC.2020.15.NUMBERS' TO F.NUMBERS ELSE
  STOP 'Cannot open AOC.2020.15.NUMBERS'
END

* Load starting numbers
CLEARFILE F.NUMBERS
FILELOCK F.NUMBERS, 'EXCLUSIVE'
FOR X = 1 TO INPUT.DATA.COUNT
  R.NUMBERS = ''
  R.NUMBERS<1> = X
  WRITE R.NUMBERS TO F.NUMBERS,INPUT.DATA<X>
NEXT

TURN = INPUT.DATA.COUNT
LAST.NUMBER = INPUT.DATA<TURN>
LOOP
  TURN += 1

  READ R.NUMBERS FROM F.NUMBERS, LAST.NUMBER THEN
    LAST.SPOKE = R.NUMBERS<1,1>
    PREV.SPOKE = R.NUMBERS<1,2>
    IF PREV.SPOKE = '' THEN
      THIS.NUMBER = 0
    END
    ELSE
      THIS.NUMBER = LAST.SPOKE - PREV.SPOKE
    END
  END
  ELSE
    THIS.NUMBER = 0
  END

  READ R.NUMBERS FROM F.NUMBERS, THIS.NUMBER ELSE
    R.NUMBERS = ''
  END
  INS TURN BEFORE R.NUMBERS<1,1>
  DEL R.NUMBERS<1,3>
  WRITE R.NUMBERS TO F.NUMBERS, THIS.NUMBER
  LAST.NUMBER = THIS.NUMBER

  IF TURN = 2020 THEN
    PART1 = THIS.NUMBER
  END
  IF TURN = 30000000 THEN
    PART2 = THIS.NUMBER
  END

* Progress indicator
  IF TURN[4] = '0000' THEN
    CRT '*':
    IF TURN[6] = '000000' THEN
      CRT ' (':TURN:')'
    END
  END
UNTIL TURN = 30000000
REPEAT

CRT "Part 1 = ":PART1
CRT "Part 2 = ":PART2
