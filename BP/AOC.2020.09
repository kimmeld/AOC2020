CALL S.GET.INPUT(2020,9,INPUT.LINES)
INPUT.LINES.COUNT = DCOUNT(INPUT.LINES, @FM)

* Part 1
PREAMBLE.LENGTH = 25
WINDOW.LENGTH = 25
FOR X = PREAMBLE.LENGTH+1 TO INPUT.LINES.COUNT
  FOUND = @FALSE
  TARGET = INPUT.LINES<X>
  WIN.START = X - WINDOW.LENGTH
  FOR Y = WIN.START TO X
    FOR Z = Y+1 TO X
      IF INPUT.LINES<Y> + INPUT.LINES<Z> = TARGET THEN
        FOUND = @TRUE
        Y = X
        Z = X
      END
    NEXT
  NEXT

  IF NOT(FOUND) THEN
    CRT 'Part 1 = ':TARGET
    X = INPUT.LINES.COUNT
  END
NEXT

* Part 2
S.PTR = 1
E.PTR = 2
CUR.SUM = INPUT.LINES<S.PTR> + INPUT.LINES<E.PTR>
CAPTURED.VALS = ''
CAPTURED.VALS<-1> = INPUT.LINES<S.PTR>
CAPTURED.VALS<-1> = INPUT.LINES<E.PTR>

LOOP
UNTIL CUR.SUM = TARGET
  IF CUR.SUM < TARGET THEN
    E.PTR += 1
    CUR.SUM += INPUT.LINES<E.PTR>
    CAPTURED.VALS<-1> = INPUT.LINES<E.PTR>
  END
  IF CUR.SUM > TARGET THEN
    CUR.SUM -= INPUT.LINES<S.PTR>
    S.PTR += 1
    DEL CAPTURED.VALS<1>
  END
REPEAT

* Find smallest/largest
LOWEST = MINIMUM(CAPTURED.VALS)
HIGHEST = MAXIMUM(CAPTURED.VALS)
CRT 'Part 2 = ':LOWEST + HIGHEST
